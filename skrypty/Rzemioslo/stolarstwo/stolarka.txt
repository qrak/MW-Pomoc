;Makro bY SabiK
;stolarka
;Wersja 1.2
;Skrypt robi ostatni przedmiot stolarki

;U W A G A !!! USTAW PONIZSZE ZMIENNE PRZED WLACZENIEM SKRYPTU !!!
set %delay 4s        ;TUTAJ USTAW DELAY PO WIEDZY O ZWIERZETACH LUB WIEDZY O BRONI ZANIM SKRYPT ZACZNIE DZIALAC (wiedza o zwierzetach ok 4s, wiesza o broni ok 2s)
set %skill 1         ;Skill [0-8] (0 - nic, 1 - Ukrywanie, 2 - Madrosc, 3 - Anatomia, 4 - Identyfikacja, 5 - Rozmowa z duchami, 6 - Wykrywanie, 7 - Wiedza o zwierzetach, 8 - Wiedza o broni)
set %skillec 1       ;Po ilu przedmiotach robic skill [1-2]
;-------------------------------------------------------------------------------------

initevents
set #sysmsgcol 1281
event sysmessage Skrypt [stolarka] odpalony (bY SabiK)
wait 1

if %skill = 0 || %skill = 1 || %skill = 5 || %skill = 6
   goto start
set %lt brak
if %skill = 2
   set %skill1 Madrosc
if %skill = 3
   set %skill1 Anatomia
if %skill = 4
   set %skill1 Identyfikacja
if %skill = 7
   set %skill1 Wiedza , #spc , o , #spc , zwierzetach
if %skill = 8
   set %skill1 Wiedza , #spc , o , #spc , broni
if %lt = brak
{
   lt:
   set #targcurs 1
   set #sysmsgcol 1090
   event sysmessage Wskaz teraz na czym ma byc robiona %skill1
   set %licz #scnt2 + 50
   repeat
   onhotkey esc
      goto dalej_lt0
   if %licz < #scnt2
      goto lt
   until #targcurs = 0
}
dalej_lt:
if anulowano in #sysmsg
   goto dalej_lt0
if #ltargetid = n/a || #ltargetkind <> 1
   goto lt
set %lt #ltargetid
set #sysmsgcol 1082
event sysmessage Zapisano.
goto start
dalej_lt0:
set %skill 0
set #sysmsgcol 1082
event sysmessage Anulowano.
goto start

start:
if *STOP = 1
{
   repeat
   until *STOP = 0
}
if %skill <> 0
{
   if %skillec = 2
   {
      if %skillecile = 1
      {
         set %skillecile 0
         goto start1
      }
      set %skillecile %skillecile + 1
   }
   gosub skill %skill
}
start1:
finditem agg_dgg_igg C
if #findkind = -1
{
   set #sysmsgcol 1080
   event sysmessage Nie masz narzedzi stolarza! Skrypt wylaczono.
   halt
}
set #lobjectid #findid
event macro 17
set %licz #scnt2 + 25
repeat
if %licz < #scnt2
   goto start
until #contsize = 560_460
klik:
click 140 390
set %licz #scnt2 + 25
repeat
if %licz < #scnt2
{
   if #contsize = 560_460
   goto klik
   else
   goto start
}
until #targcurs = 1
finditem tlk C
if #findkind = -1
{
   set #sysmsgcol 1080
   event sysmessage Nie masz drewna! Skrypt wylaczono!
   halt
}
set #ltargetid #findid
set #ltargetkind 1
event macro 22
set %jrnl #jindex
set %licz #scnt2 + 110
repeat
if #jindex > %jrnl
{
   set %jrnl %jrnl + 1
   scanjournal 1
}
until %licz < #scnt2 || s_przedmiot_i_wlozyl in #journal || s_wyjatkowej_jakosc_przedmiot_i_wlozyl in #journal || s_przedmiot_sredniej_jakosci in #journal || s_troche_materialu in #journal || Your_skill_in in #journal
goto start

sub skill
if %1 = 1
{
   ukrywanie:
   event macro 1 0 .um ukrywanie
   set %licz #scnt2 + 15
   repeat
   if %licz < #scnt2
      goto ukrywanie
   if #sysmsg = Nie_uda_Ci_sie_schowac_wierzchowca!
   {
      set %skill 0
      set #sysmsgcol 1080
      event sysmessage Nie ukryjesz sie na wierzchowcu! Skrypt nie bedzie sie ukrywal!
      return
   }
   until H in #charstatus || Nie_udalo_Ci_sie_ukryc in #sysmsg
   return
}
if %1 = 2
{
   set %jaki_skill madrosc
   gosub dokoncz_cel
   return
}
if %1 = 3
{
   set %jaki_skill anatomia
   gosub dokoncz_cel
   return
}
if %1 = 4
{
   set %jaki_skill identyfikacja
   gosub dokoncz_cel
   return
}
if %1 = 5
{
   rozmowa:
   event macro 1 0 .um rozmowa z duchami
   set %licz #scnt2 + 15
   repeat
   if %licz < #scnt2
      goto rozmowa
   until Porozumiales_sie in #sysmsg || Porozumialas_sie in #sysmsg || Nie_udalo_Ci_sie in #sysmsg
   return
}
if %1 = 6
{
   wykrywanie:
   event macro 1 0 .um wykrywanie
   set %licz #scnt2 + 15
   repeat
   if %licz < #scnt2
      goto wykrywanie
   until Znalazles_kogos in #sysmsg || Znalazlas_kogos in #sysmsg || Nie_znalazles_nikogo in #sysmsg || Nie_znalazlas_nikogo in #sysmsg
   return
}
if %1 = 7
{
   set %jaki_skill wiedza , #spc , o , #spc , zwierzetach
   gosub dokoncz_cel
   if Nie_wiesz_jak_to_ocenic in #sysmsg || Nie_udalo_ci_sie_ocenic_tego_stworzenia in #sysmsg
      return
   wait %delay
   return
}
if %1 = 8
{
   set %jaki_skill wiedza , #spc , o , #spc , broni
   gosub dokoncz_cel
   if Nie_wiesz_jak_to_ocenic in #sysmsg || Nie_udalo_ci_sie_ocenic in #sysmsg
      return
   wait %delay
   return
}
return

sub dokoncz_cel
dokoncz:
event macro 1 0 .um %jaki_skill
set %licz #scnt2 + 15
repeat
if %licz < #scnt2
   goto dokoncz
until #targcurs = 1
set #ltargetid %lt
set #ltargetkind 1
event macro 22
return
