;UZUPELNIANIE REGOW I BUTLI by Raist
;ZMODYFIKOWANE O ODKLADANIE REGOW
;Makro dobiera i odklada regi i butle do ustalonej
;wartosci zdefiniowanej w skrypcie. Oba plecaki pomiedzy
;ktorymi odbywa sie przenoszenie MUSZA byc otwarte.
;Zarowno zapas regow jak i butli MUSI byc w jednym plecaku
;Capslock w komentarzach oznacza koniecznosc
;podania wartosci.
;domyslnie ustawilem 40 - ilosc ziol pro pk orderu :/

initevents
repeat
onHotKey F11 alt    ;KLAWISZ DOUZUPELNIANIA REGOW
gosub uzupelniaj
onHotKey F12 alt   ;KLAWISZ DO ODKLADANIA
gosub odkladaj
until #false

sub uzupelniaj
ignoreitem reset
ignoreitem XNJNRYD    ;WPISZ ID WORKA WAMP DRU WILK
                      ;JESLI TAKOWY POSIADASZ

set %ziolo 320        ;ILOSC STANDARDOWYCH ZIOL DO PRZENIESIENIA
set %szafir 35        ;ILOSC SZAFIROW
set %demo 15          ;ILOSC KOSCI DEMONA
set %krew 40          ;ILOSC KRWI
set %wrzos 20         ;ILOSC CZARNEGO WRZOSU
set %fiolet 10        ;ILOSC FIOLETOW
set %zolta 25         ;ILOSC ZOLTYCH
set %czerwona 25      ;ILOSC CZERWONYCH
set %pomarancz 10     ;ILOSC POMARANCZY
set %mana 3           ;ILOSC MIKSTUR MANY
set %rubin  0         ;ILOSC RUBINOW
set %otoczak 0        ;ILOSC OTOCZKAOW
set %gdzie 0   ;start od rega 0
set #targcurs 1 ;ustawienie kursora
set #sysmsgcol 1080
event sysmessage Pokaz plecak z ktorego chcesz przelozyc ziolo
repeat ;petla czeka az wskazesz pojemnik z regami
onhotkey ESC
goto wcis_esc
until #targcurs = 0
onhotkey ESC
{
wcis_esc:
set #sysmsgcol 1080
event sysmessage ANULOWANO
return
}

set %ple1 #ltargetid  ;ustawia plecak z zapasem regow
set #lobjectid #ltargetid
set %czas #scnt2 + 20
repeat
event macro 17 0
wait 5
if %czas < #scnt2
{
set #sysmsgcol 1080
event sysmessage ANULOWANO
return
}
until ( #contid = #ltargetid )

finditem CKF_ZJF C_ , #BACKPACKID ;znajduje worek lub plecakw plecaku glownym
if #findkind = -1
   {
   set %ple2 #backpackid
   goto start
   }
set %ple2 #findid ;ustawia plecak ktory ma uzupelniac

start:
set %gdzie %gdzie + 1  ;inkrementacja - skacze po kolejnych regach
if %gdzie = 1      ;standardowe 8 regow maga
   set %reg MZF
if %gdzie = 2
   set %reg SZF
if %gdzie = 3
   set %reg KZF
if %gdzie = 4
   set %reg JZF
if %gdzie = 5
   set %reg RZF
if %gdzie = 6
   set %reg WZF
if %gdzie = 7
   set %reg KUF
if %gdzie = 8
   set %reg JUF
if %gdzie = 9
   {
   set %reg FVF        ;szafiry
   set %ziolo %szafir
   }
if %gdzie = 10
   {
   set %reg OZF         ;kosci demona
   set %ziolo %demo
   }
if %gdzie = 11
   {
   set %reg DUF       ;fiolki krwi
   set %ziolo %krew
   }
if %gdzie = 12
   {
   set %reg HUF        ;czarnywrzos
   set %ziolo %wrzos
   }
if %gdzie = 13
   {
   set %reg HVF        ;rubin
   set %ziolo %rubin
   }
if %gdzie = 14
   {
   set %reg FUF        ;otoczak
   set %ziolo %otoczak
   }
if %gdzie = 15
   {
   set %reg TUF         ;wybuch
   set %ziolo %fiolet
   }
if %gdzie = 16
   {
   set %reg UUF          ;zolta
   set %ziolo %zolta
   }
if %gdzie = 17
   {
   set %reg ZUF           ;czerwona
   set %ziolo %czerwona
   }
if %gdzie = 18
   {
   set %reg NUF             ;pomaranczowa
   set %ziolo %pomarancz
   }
if %gdzie = 19
   {
   set %reg PUF        ;potek many
   set %ziolo %mana
   }
if %gdzie = 20
   goto koniec
   
przenoszenie:
finditem %reg C_ , %ple2 ;znajduje ziolo w plecaku
set %nies %ziolo - #findstack ;wylicza ile brakuje
if %nies = 0 ;kiedy ma nie przenosic
   goto start
if %nies < 0 ;kiedy ma odlozyc
   {
   set %nies #findstack - %ziolo
   exevent drag #findid %nies ;podnies
   set %czas #scnt2 + 10
   repeat
   finditem %reg C_ , %ple2 ;znajduje ziola w twoim plecaku
   until ( #findstack = %ziolo ) || ( %czas < #scnt2 )
   finditem %reg C_ , %ple1
   set %temp %nies + #findstack
   exevent dropc %ple1 ;odloz
   set %czas #scnt2 + 10
   repeat
   finditem %reg C_ , %ple1 ;znajduje ziola w plecaku z zapasami
   until ( #findstack = %temp ) || ( %czas < #scnt2 )
   goto start
   }
finditem %reg C_ , %ple1 ;znajduje ziolo w plecaku z zapasami
if #findkind = -1 ;kiedy nie masz zapasow ziol
   {
   set #sysmsgcol 1080
   event sysmessage Nie znalazlem ziola, pomijam
   goto start
   }
set %tmp #findstack - %nies
if %tmp < 0
   {
   set %tmp 0
   set %nies #findstack
   }
exevent drag #findid %nies ;podnies
set %czas #scnt2 + 10
repeat
finditem %reg C_ , %ple1 ;znajduje ziolo w plecaku z zapasami
until ( #findstack = %tmp ) || ( %czas < #scnt2 )
finditem %reg C_ , %ple2
set %tmp #findstack + %nies
exevent dropc %ple2 ;odloz
set %czas #scnt2 + 10
repeat
finditem %reg C_ , %ple2
until ( #findstack = %tmp ) || ( %czas < #scnt2 )
goto start

koniec:
set #sysmsgcol 1063
event sysmessage Skonczylem przekladac
return

sub odkladaj
set #targcurs 1 ;ustawienie kursora
set #sysmsgcol 1080
event sysmessage Pokaz plecak do ktorego chcesz odlozyc ziola i kamienie
repeat ;petla czeka az wskazesz pojemnik z regami
onhotkey ESC
goto wcis_esc2
until #targcurs = 0
onhotkey ESC
{
wcis_esc2:
set #sysmsgcol 1080
event sysmessage ANULOWANO
return
}
set %ple #ltargetid
set #lobjectid #ltargetid
set %czas #scnt2 + 10
repeat
event macro 17 0
wait 5
if %czas < #scnt2
{
set #sysmsgcol 1080
event sysmessage ANULOWANO
return
}
until ( #contid = #ltargetid )

lecimy:
finditem szf_rzf_mzf_kzf_wzf_juf_jzf_ozf_vzf_yzf_tzf_qzf_duf_pzf_fuf_euf_nzf_huf_eag_guf_iuf_kuf_uzf_dag_eth_lzf_rvf_mwf_evf_bvf_gvf_ovf_fvf_hvf_vuf_wuf C_ , #backpackid ;
if #findkind = -1
{
set #sysmsgcol 1063
event sysmessage Odlozone
return
}
set %id_przen #findid
set %stack_przen #findstack
set %typ_przen #findtype
exevent drag %id_przen %stack_przen ;podnies
set %czas #scnt2 + 10
repeat
finditem %id_przen C_ , #backpackid ;
until ( #findkind = -1 ) || ( %czas < #scnt2 )
finditem %typ_przen C_ , %ple
set %tmp #findstack
exevent dropc %ple ;odloz
set %czas #scnt2 + 10
repeat
finditem #findtype C_ , %ple
until ( #findstack <> %tmp ) || ( %czas < #scnt2 )
goto lecimy




