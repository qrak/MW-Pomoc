Kod:
; magery ...

; 1 - wlaczony, 0 - wylaczony
set %BLESS 1
set %JEDZ 1
; bedzie jadlo ponizsze zarcie
set %zarcie HND_END_QQD_QUD_YRD
; od %proc many bedzie medytowac...
set %proc 40

set %timeout 15
set %ZARCIE_SCNT #SCNT
set %blessed 0

loop:
if %START = N/A
   {
   set %sint #Int
   gosub start
   set %START KRULEWNA_SNIERZKA
   }
if %ZARCIE_SCNT <= #SCNT && %JEDZ = 1
   {
   gosub spr_glod
   set %ZARCIE_SCNT #SCNT + 300
   }
set %mana #maxmana * %proc / 100
if #mana <= %mana
   gosub medit
if %INT > #INT
   {
   set %INT #INT
   set %blessed 0
   }
if ( #INT = %INT && %blessed = 0 ) || ( #INT = %INT + 1 && %blessed = 0 )
   {
   set %INT #INT
   repeat
         {
         set %JIndex #JIndex
         event macro 15 9
         gosub ScanJournal %JIndex Czar_sie_nie_udal. Nie_masz_odpowiedniej already You_must_wait
         if #Result = 3
            gosub medit
         }
   until #Result = TARG
   event macro 23 0
   wait 20
   }
;--------------------------------
chooseskill Mage
if #skill <= 330
   {
   display ok Chuj wie co czarowac przy takim skillu. Halt:(
   halt
   }
if #skill > 330
   set %czar 24
if #skill > 500
   set %czar 25
if #skill > 600
   set %czar 39
if #skill > 720
   set %czar 47
if #skill > 799
   set %czar 42
if #skill > 884
   set %czar 51
;--------------------------------
repeat
      {
      set %JIndex #JIndex
      set #LSpell %czar
      event macro 16 0
      gosub ScanJournal %JIndex Czar_sie_nie_udal. Nie_masz_odpowiedniej already You_must_wait
      ;                 1        2                  3                     4       5
      if #Result = 2 || #Result = 4 || #Result = 5
         wait 5
      if #Result = 3
         gosub medit
      }
until #Result = TARG
target
if %czar = 24
   {
   set #LTargetX #CharPosX
   set #LTargetY #CharPosY
   set #LtargetZ #CharPosZ
   set #LTargetKind 2
   event macro 22 0
   }
else
if %czar = 25
   {
   event macro 23 0
   }
else
if %czar = 39 || %czar = 47 || %czar = 37
    {
   set #LTargetX #CharPosX
   set #LTargetY #CharPosY + 2
   set #LtargetZ #CharPosZ
   set #LTargetKind 2
    event macro 22 0
    }
else
    {
    set #LTargetID %target
    set #LTargetKind 1
    event macro 22 0
    }
goto loop


sub start
set #TargCurs 1
event sysmessage Wskaz w co chcesz czarowac...
repeat
until #TargCurs = 0
set %target #LTargetID
event sysmessage Wskazano.
if %BLESS = 1
   {
   event sysmessage Makro bedzie rzucalo blesa...
   set %INT #INT
   repeat
         {
         set %JIndex #JIndex
         event macro 15 9
         gosub ScanJournal %JIndex Czar_sie_nie_udal. Nie_masz_odpowiedniej already You_must_wait
         if #Result = 3
            gosub medit
         }
   until #Result = TARG
   event macro 23 0
   wait 2s
   if #INT = %INT
      set %blessed 1
   }
return


sub medit
if #Mana = #MaxMana
   return
repeat
      {
      event macro 13 46
      wait 5s
      }
until #Mana = #MaxMana
return


sub spr_glod
glodloop:
repeat
      event macro 8 7
      wait 5
until #ContID = #BackpackID
wait 30
set %_JIndex #JIndex
event macro 1 0 .glod
gosub ScanJournal %_JIndex Twoja_postac_jest_tak_najedzona Twoja_postac_jest_najedzona_do_syta_i_nic_wiecej Twoja_postac_jest_najedzona_do_syta Twoja_postac_jest_najedzona Twoja_postac_nie_jest_glodna Twoja_postac_czuje_oznaki_glodu Twoja_postac_jest_nieco_glodna Twoja_postac_jest_glodna Twoja_postac_jest_bardzo_glodna Twoja_postac_przymiera_glodem Twoja_postac_jest_bliska_smierci_glodowej
;                           2                               3                                                4                                   5                           6                            7                               8                              9                        10                              11                            12
;if #Result < 4
;   event macro 1 0 .glodniej
;if #Result = 4 || #Result = 5
;   return
if #Result <= 5
   return
if #Result > 5
   {
   FindItem %zarcie C
   if #FindKind = -1
      {
      event sysmessage Nie znalazle jedzenia:(
      return
      }
   set #LObjectID #FindID
   event macro 17 0
   }
goto glodloop
return


sub ScanJournal
set %__JIndex %1
set %_timeout #SCNT
ScanLoop:
if #SCNT > %_timeout + %timeout
   return TIMEOUT
if #TargCurs = 1
   return TARG
if #JIndex > %__JIndex
   {
   set %__JIndex %__JIndex + 1
   ScanJournal %__JIndex
   for %__i 2 %0
       {
       if % . %__i in #journal
          return %__i
       }
   }
goto ScanLoop
return


